plugins {
    id "java"
    id "eclipse"
    id "idea"
    id "edu.wpi.first.GradleRIO" version "2018.06.21"
}

def ROBOT_CLASS = "org.redalert1741.testbed2.Robot"

// Define my targets (RoboRIO) and artifacts (deployable files)
// This is added by GradleRIO's backing project EmbeddedTools.
deploy {
    targets {
        target("roborio", edu.wpi.first.gradlerio.frc.RoboRIO) {
            team = Integer.parseInt(TEAM)
        }
    }
    artifacts {
        artifact('frcJava', edu.wpi.first.gradlerio.frc.FRCJavaArtifact) {
            targets << "roborio"
        }

        fileArtifact('logging.properties') {
            file = file('logging.properties')
            filename = 'logging.properties'
            targets << "roborio"
        }

        fileCollectionArtifact('autoFiles') {
            directory = 'auto'
            files = fileTree(dir: 'auto')
            targets << "roborio"
        }
    }
}

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

tasks.discoverRoborio.doFirst { println "Connecting to: " + TEAM }

// Defining my dependencies. In this case, WPILib (+ friends), CTRE Toolsuite (Talon SRX)
// and NavX.
dependencies {
    compile wpilib()
    compile ctre()
    compile navx()
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'
    // compile group: 'org.apache.logging.log4j.jul.LogManager'

}

// Setting up my Jar File. In this case, adding all libraries into the main jar ('fat jar')
// in order to make them all available at runtime. Also adding the manifest so WPILib
// knows where to look for our Robot Class.
jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest edu.wpi.first.gradlerio.GradleRIOPlugin.javaManifest(ROBOT_CLASS)
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4'
}
